
buildscript {
    repositories {
        jcenter()
    }
}
plugins{
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}


apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'de.fuerstenau.buildconfig'

group = 'org.rundeck.api'

/**
 * Generates java source with embedded version info
 */
buildConfig {
    clsName = 'Version'
    packageName = 'org.rundeck.client'
}


dependencies {

    compileOnly "org.projectlombok:lombok:1.16.16"

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
    compile 'com.squareup.retrofit2:converter-simplexml:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.5.0'


    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.squareup.retrofit2:retrofit-mock:2.1.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.5.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile "org.codehaus.groovy:groovy-all:2.3.7"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}


// produce a jar file for our test classes
task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.java
}

// produce a jar file for our source files
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

// produce a jar file for our javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// add all of the artifacts above to our archives list
artifacts {
    archives testJar
    archives sourceJar
    archives javadocJar
}
ext.pomClosure =  {
    project {
        artifactId project.name
        groupId project.group
        inceptionYear '2017'
        packaging 'jar'
        version version
        name "Rundeck API Library ${project.version}"
        description project.description ?: 'Rundeck API Library'
        url 'http://rundeck.org'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/rundeck/rundeck-cli'
            connection 'scm:git:git@github.com/rundeck/rundeck-cli.git'
            developerConnection 'scm:git:git@github.com:rundeck/rundeck-cli.git'
        }
        developers {
            developer {
                id('gschueler')
                name('Greg Schueler')
                email('greg@rundeck.com')
            }
        }
        parent {
            groupId('org.sonatype.oss')
            artifactId('oss-parent')
            version('7')
        }
    }
}
//
//install{
//    repositories.mavenInstaller {
//        pom(pomClosure)
//    }
//}

task('createPom').doLast {
    pom(pomClosure).writeTo("${buildDir}/poms/${project.name}-${project.version}.pom")
}
